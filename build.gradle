plugins {
  id "com.jfrog.bintray" version "1.3.1"
}

allprojects {
  repositories {
    jcenter()
  }
  apply plugin: 'maven'
  apply plugin: 'maven-publish'
  apply plugin: 'java'
}

group = 'com.github.geowarin'
version = '1.1.0'

description = 'A junit rule to run docker containers'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.slf4j:slf4j-api:1.7.12'
  compile 'com.spotify:docker-client:3.4.0:shaded'
  compile('junit:junit:4.12') { ext.optional = true }
  compile 'com.google.guava:guava:18.0'
  testCompile 'org.slf4j:slf4j-simple:1.7.12'
  testCompile 'com.rabbitmq:amqp-client:3.2.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar, javadocJar
}

publishing {
  publications {
    JarPublication(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      pom.withXml {
        def depMap = configurations.compile.dependencies.collectEntries { [it.name, it] }
        asNode().dependencies.dependency.findAll {
          def dep = depMap.get(it.artifactId.text())
          return dep?.hasProperty('optional') && dep.optional
        }.each {
          if (it.optional) {
            it.optional.value = 'true'
          } else {
            it.appendNode('optional', 'true')
          }
        }
      }
    }
  }
}

def bintrayUser = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
def bintrayKey = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')

bintray {
  user = bintrayUser
  key = bintrayKey
  publications = ['JarPublication']
  pkg {
    repo = 'maven'
    name = project.name
    desc = project.description
    licenses = ['MIT']
    vcsUrl = 'https://github.com/geowarin/docker-junit-rule.git'
    version {
      name = project.version
      desc = project.description
    }
  }
}
